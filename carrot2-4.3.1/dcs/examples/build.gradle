
apply plugin: 'java'

repositories {
  mavenCentral()

  def localRepo = file('../../artifacts')
  if (localRepo.isDirectory()) {
    maven {
      name 'local'
      url localRepo
    }
  }
}

dependencies {
  implementation "org.carrot2:carrot2-core:4.3.1"
  implementation "org.carrot2.dcs:carrot2-dcs-model:4.3.1"

  implementation "com.fasterxml.jackson.core:jackson-databind:2.12.0"
  implementation "org.apache.httpcomponents:httpclient:4.5.13"
  implementation "com.carrotsearch.console:launcher:1.0.4"
}

jar {
  doFirst {
    manifest {
      attributes("Main-Class": "com.carrotsearch.console.launcher.Launcher")
      attributes("Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
    }
  }
}

task copyJars(type: Sync) {
  from(configurations.runtimeClasspath)
  from(jar)

  into "$buildDir/assembled"
}

assemble.dependsOn copyJars
